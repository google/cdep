apply plugin: 'java'
apply plugin: 'kotlin'
group 'io.cdep'


repositories {
    jcenter()
}

buildscript {
    ext.kotlin_version = '1.2.30'
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


if (System.getenv()["TRAVIS_TAG"] != null && System.getenv()["TRAVIS_TAG"].length() > 0) {
    version System.getenv()["TRAVIS_TAG"]
} else {
    version 'dev'
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources"
        }
    }
}


class GenerateBuildInfo extends DefaultTask {
    @OutputFile
    public File buildInfoFile = new File("cdep/src/main/java/io/cdep/BuildInfo.java")

    @TaskAction
    generate() {
        buildInfoFile.parentFile.mkdirs()
        buildInfoFile.write(
                """package io.cdep;
class BuildInfo {
  static final String PROJECT_VERSION = "$project.version";
}""")
    }
}

task generateBuildInfo(type: GenerateBuildInfo)
compileJava.dependsOn generateBuildInfo

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    //noinspection GroovyAssignabilityCheck
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


dependencies {
    compile 'org.yaml:snakeyaml:1.26'
    compile group: 'org.fusesource.jansi', name: 'jansi', version: '1.15'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'com.google.guava', name: 'guava', version: '20.0'
    testCompile group: 'com.google.truth', name: 'truth', version: '0.31'
    testCompile "org.mockito:mockito-core:2.+"
    testCompile group: 'net.java', name: 'quickcheck', version: '0.6'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
